# Maintainer: TNE <TNE[at]EosOS[dot]org>
# Contributor: Philip Müller <philm[at]manjaro[dog]org>
# https://gitlab.manjaro.org/manjaro-arm/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares/-/blob/master/PKGBUILD
# https://gitlab.manjaro.org/packages/extra/calamares-git/-/blob/master/PKGBUILD

pkgname=calamares-eo
pkgver=3.3.14.r1.gc375d926ce
_commit=c375d926ce2f67844bbd95e6734c1806150c5192
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
conflicts=('core-calamares')
url="https://codeberg.org/m4teo/calamares"
license=('GPL' 'LGPL')
conflicts=('calamares')
depends=(
  'boost-libs'
  'dmidecode'
  'gptfdisk'
  'hwinfo'
  'kconfig'
  'kcoreaddons'
  'kcrash'
  'kiconthemes'
  'ki18n'
  'kparts'
  'kpmcore'
  'kservice'
  'kwidgetsaddons'
  'mkinitcpio-openswap'
  'networkmanager'
  'polkit-qt6'
  'python'
  'python-distutils-extra'
  'qt6-base'
  'qt6-declarative'
  'qt6-svg'
  'rsync'
  'solid'
  'squashfs-tools'
  'yaml-cpp'
)
makedepends=(
  'boost'
  'cmake'
  'doxygen'
  'extra-cmake-modules'
  'git'
  'ninja'
  'python-jsonschema'
  'python-pyaml'
  'python-unidecode'
  'qt6-tools'
)

source=(git+https://codeberg.org/m4teo/calamares.git#commit=${_commit} \
        "calamares.desktop" \
        "calamares_polkit" \
        "49-nopasswd-calamares.rules")

sha256sums=('SKIP'
            'SKIP'
            'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')

pkgver() {
  cd "$srcdir/calamares" || exit

  _regex='^set\(CALAMARES_VERSION ([0-9]+\.[0-9]+\.[0-9]+([^0-9].*)?)\)\s*$'
  _file='CMakeLists.txt'

  _line=$(
    grep -E "$_regex" "$_file" \
      | head -1
  )
  _version=$(
    printf '%s' "$_line" | sed -E "s@$_regex@\1@;s@alpha@a@;s@beta@b@;s@-@.@"
  )
  _line=$(
    printf '%s' "$_line" \
      | sed -E 's@\(@\\(@;s@\)@\\)@'
  )
  _commit=$(
    git log -G "$_line" -1 --pretty=oneline --no-color | sed 's@\ .*$@@'
  )
  _revision=$(
    git rev-list --count "$_commit"..HEAD
  )
  _hash=$(
    git rev-parse --short HEAD
  )

  printf '%s.r%s.g%s' \
    "$_version" \
    "$_revision" \
    "$_hash"
}

prepare() {
        cd ${srcdir}/calamares
        sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py
        # Forzar crc32c genérico para Btrfs
        # sed -i 's/modules.append("crc32c-intel" if cpuinfo().is_intel else "crc32c")/modules.append("crc32c")/g' src/modules/initcpiocfg/main.py
}

build() {
  cd "${srcdir}/calamares" || exit

  mkdir -p build && cd build || exit
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_PYBIND11=OFF \
    -DWITH_QT6=ON \
    -DBUILD_TESTING=OFF \
    -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg packagechooser packagechooserq \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc"
  make
}

package() {
  cd ${srcdir}/calamares/build
  DESTDIR="${pkgdir}" cmake --build . --target install
  install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
  install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
  install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
  chmod 750 "$pkgdir/etc/polkit-1/rules.d"
}
